
#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма); 
		
	ИначеЕсли ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаСписка" Тогда
		
		ДобавитьСтолбецКонтактноеЛицоПослеКонтрагента(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровИУслуг.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);

	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
		
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
		
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)

	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.АШ_КонтактноеЛицо";	
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	 ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	 ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	 ПолеВвода.ПутьКДанным = "Объект.АШ_КонтактноеЛицо";	 
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.АШ_СогласованнаяСкидка";	
	
КонецПроцедуры

Процедура ДобавитьСтолбецКонтактноеЛицоПослеКонтрагента(Форма)
	
	//АШ Вставить колонку КонтактноеЛицо в форму списка документа Заказ покупателя
	
	Форма.Список.ТекстЗапроса = "ВЫБРАТЬ
	|ДокументЗаказПокупателя.Ссылка КАК Ссылка,
	|ДокументЗаказПокупателя.ПометкаУдаления КАК ПометкаУдаления,
	|ДокументЗаказПокупателя.Номер КАК Номер,
	|ДокументЗаказПокупателя.Дата КАК Дата,
	|ДокументЗаказПокупателя.Проведен КАК Проведен,
	|ДокументЗаказПокупателя.Организация КАК Организация,
	|ДокументЗаказПокупателя.Контрагент КАК Контрагент,
	|ДокументЗаказПокупателя.Договор КАК Договор,
	|ДокументЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	|ДокументЗаказПокупателя.Ответственный КАК Ответственный,
	|ДокументЗаказПокупателя.Комментарий КАК Комментарий,
	|ДокументЗаказПокупателя.МоментВремени КАК МоментВремени,
	|ВЫБОР
	|	КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
	|		ТОГДА 0
	|	КОГДА НаличиеФайлов.ЕстьФайлы
	|		ТОГДА 1
	|	ИНАЧЕ 0
	|КОНЕЦ КАК ЕстьФайлы,
	
	//АШ В список добавлена выборка Контактного лица
	
	|ДокументЗаказПокупателя.АШ_КонтактноеЛицо КАК АШ_КонтактноеЛицо
	|ИЗ
	|Документ.ЗаказПокупателя КАК ДокументЗаказПокупателя
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
	|	ПО ДокументЗаказПокупателя.Ссылка = НаличиеФайлов.ОбъектСФайлами";
	
	СтолбецКонтактныеЛица = Форма.Элементы.Добавить("АШ_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.Список);
	СтолбецКонтактныеЛица.Вид = ВидПоляФормы.ПолеНадписи;
	СтолбецКонтактныеЛица.Заголовок = "Контактное лицо";
	СтолбецКонтактныеЛица.ПутьКДанным = "Список.АШ_КонтактноеЛицо";
	
КонецПроцедуры

#КонецОбласти